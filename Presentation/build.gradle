apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'com.frogermcs.androiddevmetrics'

buildscript {
  repositories {
    jcenter()
    mavenCentral()
  }

  dependencies {
    classpath 'com.frogermcs.androiddevmetrics:androiddevmetrics-plugin:0.4'
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
  }
}

android {

  compileSdkVersion 24
  buildToolsVersion '24.0.3'

  defaultConfig {
    applicationId "com.smartdengg.presentation"
    minSdkVersion 21
    targetSdkVersion 24
    versionCode 1
    versionName "1.0"

    renderscriptTargetApi 19
    renderscriptSupportModeEnabled true
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      debuggable false
    }
    debug {
      manifestPlaceholders = [debuglable: "${rootProject.ext.debugLable}"]
      debuggable true
      applicationIdSuffix ".debug"
      versionNameSuffix '.dev'
    }
  }
}

// Trigger build type (as soon as possible) and make some action via corresponding tasks
project.afterEvaluate {
  tasks.all { task ->
    if (task.name =~ /check.*Manifest/) {
      if (task.name =~ /[dD]ebug/) {
        task.dependsOn(onDebugTypeTriggered)
      } else {
        task.dependsOn(onReleaseTypeTriggered)
      }
    }
  }
}

task onDebugTypeTriggered << {
  rootProject.ext.variantRelease = false
}

task onReleaseTypeTriggered << {
  rootProject.ext.variantRelease = true
}

dependencies {
  compile fileTree(include: ['*.jar'], dir: 'libs')
  /*Google Library*/
  compile 'com.android.support:design:24.2.0'
  compile 'com.android.support:cardview-v7:24.2.0'
  /*Square Library*/
  compile 'com.jakewharton:butterknife:8.4.0'
  apt 'com.jakewharton:butterknife-compiler:8.4.0'
  /*ReactiveX Library*/
  compile 'com.trello:rxlifecycle-components:0.4.0'
  compile 'io.reactivex:rxandroid:1.1.0'
  compile 'com.jakewharton.rxbinding:rxbinding-recyclerview-v7:0.4.0'
  compile 'com.github.SmartDengg:RxDebounceClick:0.0.1'
  /*Facebook Library*/
  debugCompile 'com.facebook.stetho:stetho-okhttp3:1.3.0'
  debugCompile 'com.facebook.stetho:stetho:1.3.0'
  compile project(':Model')
}
